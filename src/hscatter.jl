# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENCE in the project root.
# ------------------------------------------------------------------

"""
    hscatter(data, varâ‚, varâ‚‚)

H-scatter plot of geospatial `data` for pair of variables
`varâ‚` and `varâ‚‚` with additional options:

* `lag`           - lag distance between points (default to `0.0`)
* `tol`           - tolerance for lag distance (default to `1e-1`)
* `distance`      - distance from Distances.jl (default to `Euclidean()`)

Aesthetics options:

* `size`          - size of points in point set
* `color`         - color of geometries or points
* `alpha`         - transparency channel in [0,1]
* `colorscheme`   - color scheme from ColorSchemes.jl
* `rcolor`        - color of regression line
* `icolor`        - color of identity line
* `ccolor`        - color of center lines

## Examples

```
# h-scatter of Z vs. Z at lag 1.0
hscatter(data, :Z, :Z, lag=1.0)

# h-scatter of Z vs. W at lag 2.0
hscatter(data, :Z, :W, lag=2.0)
```
"""
@Makie.recipe(HScatter, data, varâ‚, varâ‚‚) do scene
  Makie.Attributes(;
    # h-scatter options
    lag           = 0.0,
    tol           = 1e-1,
    distance      = Euclidean(),

    # aesthetics options
    size          = Makie.theme(scene, :markersize),
    color         = :slategray3,
    alpha         = 1.0,
    colorscheme   = :viridis,
    rcolor        = :brown,
    icolor        = :black,
    ccolor        = :green,
  )
end

function Makie.plot!(plot::HScatter)
  # retrieve data and variables
  data = plot[:data][]
  varâ‚ = plot[:varâ‚][]
  varâ‚‚ = plot[:varâ‚‚][]

  # retrieve h-scatter options
  lag      = plot[:lag][]
  tol      = plot[:tol][]
  distance = plot[:distance][]

  # retrieve aesthetics options
  size        = plot[:size][]
  color       = plot[:color][]
  alpha       = plot[:alpha][]
  colorscheme = plot[:colorscheme][]
  rcolor      = plot[:rcolor][]
  icolor      = plot[:icolor][]
  ccolor      = plot[:ccolor][]

  # process color spec into colorant
  colorant = MeshViz.process(color, colorscheme, alpha)

  # lookup valid data
  Iâ‚ = findall(!ismissing, getproperty(data, varâ‚))
  Iâ‚‚ = findall(!ismissing, getproperty(data, varâ‚‚))
  ğ’®â‚ = view(data, Iâ‚)
  ğ’®â‚‚ = view(data, Iâ‚‚)
  ğ’Ÿâ‚ = domain(ğ’®â‚)
  ğ’Ÿâ‚‚ = domain(ğ’®â‚‚)
  Xâ‚ = [coordinates(centroid(ğ’Ÿâ‚, i)) for i in 1:nelements(ğ’Ÿâ‚)]
  Xâ‚‚ = [coordinates(centroid(ğ’Ÿâ‚‚, i)) for i in 1:nelements(ğ’Ÿâ‚‚)]
  zâ‚ = getproperty(ğ’®â‚, varâ‚)
  zâ‚‚ = getproperty(ğ’®â‚‚, varâ‚‚)

  # compute pairwise distance
  m, n = length(zâ‚), length(zâ‚‚)
  pairs = [(i,j) for j in 1:n for i in j:m]
  ds = [evaluate(distance, Xâ‚[i], Xâ‚‚[j]) for (i,j) in pairs]

  # find indices with given lag
  match = findall(abs.(ds .- lag) .< tol)

  if isempty(match)
    throw(ErrorException("no points were found with lag = $lag, aborting..."))
  end

  # visualize h-scatter
  mpairs = view(pairs, match)
  x = zâ‚[first.(mpairs)]
  y = zâ‚‚[last.(mpairs)]
  Makie.scatter!(plot, x, y,
    color      = colorant,
    markersize = size,
  )

  # visualize regression line
  X = [x ones(length(x))]
  yÌ‚ = X * (X \ y)
  Makie.lines!(plot, x, yÌ‚,
    color = rcolor,
  )

  # visualize identity line
  xmin, xmax = extrema(x)
  ymin, ymax = extrema(y)
  vmin = min(xmin, ymin)
  vmax = max(xmax, ymax)
  Makie.lines!(plot, [vmin, vmax], [vmin, vmax],
    color = icolor,
  )

  # visualize center lines
  xÌ„, yÌ„ = mean(x), mean(y)
  Makie.lines!(plot, [xÌ„, xÌ„], [vmin, vmax],
    color = ccolor,
  )
  Makie.lines!(plot, [vmin, vmax], [yÌ„, yÌ„],
    color = ccolor,
  )
  Makie.scatter!(plot, [xÌ„], [yÌ„],
    color = ccolor,
    marker = :rect,
    markersize  = 16,
  )
end